! THIS PROGRAM TAKES AN ADCIRC FILE, AND OUTPUTS TELEMAC'S IPOBO ARRAY
! AS A TEXT FILE; THE CODE IN THIS FILE IS A STRIPPED DOWN VERSION OF
! THE ORIGINAL CODE EXTRACTED FROM V7P2R3 OF STBTEL (WHICH IS PART OF
! THE OPEN TELEMAC-MASCARET SYSTEM). MORE SPECIFICALLY, IT INCLUDES
! SOURCE CODE FROM LECADC.F, VOISIN.F, AND RANBO.F; NOTE THAT SHAPES OF
! CERTAIN ARRAYS WERE MODIFIED COMPARED TO THE ORIGINAL CODE.
! 
! TWO COMMAND LINE INPUTS ARE REQUIRED: 
! ./BND_EXTR_STBTEL MESH.GRD MESH_IPOBO.TXT
!
! WHERE:
! INPUT FILE NAME (MESH.GRD)
! OUTPUT FILE NAME (MESH_IPOBO.TXT)
! 
! BY PAT PRODANOVIC, PH.D., P.ENG.
! DEC 19, 2017
!
PROGRAM BND_EXTR_STBTEL
  IMPLICIT NONE
      
  ! VARIABLE DEFINITIONS (COUNTERS)
  INTEGER :: I, J, K, L

  ! THESE ARE THE PROPERTIES OF THE MESH, TO BE READ FROM THE ADCIRC FILE
  INTEGER :: NPOIN, NELEM, NPMAX, NELMAX, IBID
  DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:) :: X, Y, ZF
  INTEGER, ALLOCATABLE, DIMENSION(:,:) :: IKLE
  CHARACTER(LEN=80) :: DUMMY_TEXT
  
  ! THIS IS FOR FILE READING
  CHARACTER(LEN=80) :: ARG
  INTEGER :: ARG_NUM
  
  ! WE WILL HAVE TWO INPUTS, FILES(1) IS INPUT AND FILES(2) IS OUTPUT
  CHARACTER(LEN=80),DIMENSION(2) :: FILES

  ! THESE ARE FOR THE VOISIN SUBROUTINE
  INTEGER, ALLOCATABLE, DIMENSION(:,:) :: IFABOR
  INTEGER, ALLOCATABLE, DIMENSION(:) :: NVOIS

  ! LOCAL DECLARATIONS
  INTEGER :: NFACE,NDP,KEL,IMAX,IFACE,IELEM,M1,M2,IV,IELEM2,IFACE2
  INTEGER :: ERR,IR1,IR2,IR3,IR4,I1,I2,IDIMAT
  INTEGER :: SOMFAC(2,4,2)
  DATA SOMFAC / 1,2 , 2,3 , 3,1 , 0,0   ,  1,2 , 2,3 , 3,4 , 4,1 /
  INTEGER, ALLOCATABLE, DIMENSION(:) :: MAT1,MAT2,MAT3, IADR

  ! THESE ARE LOCAL DECLARATIONS FROM RANBO.F
  INTEGER IILE, NILE
  INTEGER, ALLOCATABLE, DIMENSION(:,:) :: TRAV1
  INTEGER ISUIV, NPTFR, NOEUD1, NOEUD2
  INTEGER SOMSUI(4), IERROR
  DOUBLE PRECISION SOM1, SOM2, Y2, EPSILO
  LOGICAL SWAP
  DATA SOMSUI / 2 , 3 , 4 , 0 /
  DATA EPSILO / 1.D-6 /

  ! I NEED TO DEFINE AND ALLOCATE NBOR AND KP1BOR (ANSWER WAS IN STBTEL.F) 
  INTEGER, ALLOCATABLE, DIMENSION (:) :: NBOR, KP1BOR
  
  ! GET THE NUMBER OF ARGUMENTS, AND ASSIGN COMMAND LINE INPUT TO ARRAY FILES
  ARG_NUM = IARGC()
  DO I = 1, ARG_NUM
    CALL GETARG(I,ARG)
    FILES(I) = ARG
  END DO
  
  IF (ARG_NUM == 2) THEN
    OPEN(UNIT=1, FILE=FILES(1), STATUS='OLD')
    OPEN(UNIT=2, FILE=FILES(2), STATUS='UNKNOWN')
  ELSE
    WRITE(*,*) 'WRONG NUMBER OF INPUT ARGUMENTS!'
    WRITE(*,*) 'ENTER INPUT FILE NAME, AND OUTPUT FILE NAME!!'
    CALL EXIT(0)
  END IF

  ! #########################################################################
  ! NOW READ THE ADCIRC FILE
  READ(1,*) DUMMY_TEXT
  
  ! READ THE NELEM AND NPOIN FROM THE ADCIRC FILE
  READ(1,*) NELEM, NPOIN
  
  ! NOW WE CAN COMPUTE NPMAX AND NELEMAX
  NPMAX = NPOIN + 0.1*NELEM
  NELMAX = NELEM + 0.2*NELEM

  ! ALLOCATE THE VARIABLES NOW
  ALLOCATE(IKLE(NELEM,3), X(NPOIN), Y(NPOIN), ZF(NPOIN))
  ALLOCATE(IADR(NPOIN), IFABOR(NELEM,3), NVOIS(NPOIN))
  ALLOCATE(TRAV1(NPOIN,2))
  ALLOCATE(NBOR(NPMAX), KP1BOR(NPMAX))
  
  ! THIS HERE STARTS BY READING IN THE ADCIRC INPUT FILE
  ! NOW WE ARE READY TO READ IN THE NODAL COORDINATES
  DO I=1,NPOIN
    READ(1,*) J,X(I),Y(I),ZF(I)
    !WRITE(2,*) J,X(I),Y(I),ZF(I)
    IF(I.NE.J) THEN
      WRITE(*,*) 'ERROR IN THE LIST OF COORDINATES LINE ',I
      CALL EXIT(0)
    ENDIF
  ENDDO

  ! NOW WE READ IN THE ELEMENT CONNECTIVITIES
  DO I=1,NELEM
    READ(1,*) J,IBID,IKLE(I,1),IKLE(I,2),IKLE(I,3)
    !WRITE(2,*) J,IBID,IKLE(I,1),IKLE(I,2),IKLE(I,3)
  ENDDO

  ! #########################################################################
  ! THE CODE BELOW IS EXTRQCTED FROM THE SUBROUTINE VOISIN.F FROM BIEF
  
  !TRIANGLES
  NFACE = 3
  !NUMBER OF POINTS PER ELEMENT
  NDP = 3
  !ADDRESS IN SOMFAC
  KEL = 1

  !IDIMAT IS BIGGER THAN THE SUM OF THE NUMBER OF NEIGHBOURS OF
  !ALL THE POINTS (NEIGHBOUR = CONNECTED BY A SEGMENT)
  IDIMAT = NDP*2*NELEM

  ALLOCATE(MAT1(IDIMAT))
  ALLOCATE(MAT2(IDIMAT))
  ALLOCATE(MAT3(IDIMAT))

  !ARRAY NVOIS FOR EACH POINT
  !BEWARE : NVOIS IS BIGGER THAN THE ACTUAL NUMBER OF NEIGHBOURS
  !THE SUM OF NVOIS WILL GIVE IDIMAT

  DO I=1,NPOIN
    NVOIS(I) = 0
  ENDDO

  DO IFACE = 1,NFACE
    DO IELEM=1,NELEM
      I1 = IKLE( IELEM , SOMFAC(1,IFACE,KEL) )
      I2 = IKLE( IELEM , SOMFAC(2,IFACE,KEL) )
      NVOIS(I1) = NVOIS(I1) + 1
      NVOIS(I2) = NVOIS(I2) + 1
    ENDDO
  ENDDO

  ! ADDRESSES OF EACH POINT IN A STRUCTURE OF TYPE COMPACT MATRIX
  IADR(1) = 1
  DO I= 2,NPOIN
    IADR(I) = IADR(I-1) + NVOIS(I-1)
  ENDDO ! I

  IMAX = IADR(NPOIN) + NVOIS(NPOIN) - 1

  ! INITIALISES THE COMPACT MATRIX TO 0
  DO I=1,IMAX
    MAT1(I) = 0
  ENDDO

  !LOOP ON THE SIDES OF EACH ELEMENT:
  DO IFACE = 1, NFACE
    DO IELEM = 1, NELEM
      IFABOR(IELEM,IFACE) = -1
      !GLOBAL NODE NUMBERS FOR THE SIDE:
       
      I1 = IKLE( IELEM , SOMFAC(1,IFACE,KEL) )
      I2 = IKLE( IELEM , SOMFAC(2,IFACE,KEL) )
       
      !ORDERED GLOBAL NUMBERS:
       
      M1 = MIN0(I1,I2)
      M2 = MAX0(I1,I2)

      DO IV = 1,NVOIS(M1)
         IF(MAT1(IADR(M1)+IV-1).EQ.0) THEN
           MAT1(IADR(M1)+IV-1)=M2
            MAT2(IADR(M1)+IV-1)=IELEM
            MAT3(IADR(M1)+IV-1)=IFACE
            GO TO 81
          ELSEIF(MAT1(IADR(M1)+IV-1).EQ.M2) THEN
            IELEM2 = MAT2(IADR(M1)+IV-1)
            IFACE2 = MAT3(IADR(M1)+IV-1)
            IFABOR(IELEM,IFACE) = IELEM2
            IFABOR(IELEM2,IFACE2) = IELEM
            GO TO 81
         ENDIF
      ENDDO !IV
81    CONTINUE

    ENDDO ! IELEM
  ENDDO ! IFACE

  DEALLOCATE(MAT1)
  DEALLOCATE(MAT2)
  DEALLOCATE(MAT3)
  ! #########################################################################
  ! THE CODE BELOW IS EXTRQCTED FROM THE SUBROUTINE RANBO.F FROM STBTEL
  ! COMMENTS IN FRENCH WERE REMOVED

  SOMSUI(NDP) = 1
  NPTFR = 0
  DO IELEM=1,NELEM
     DO IFACE=1,NDP
        IF (IFABOR(IELEM,IFACE).LE.0) THEN
           NPTFR = NPTFR + 1
           TRAV1(NPTFR,1) = IKLE(IELEM,       IFACE )
           TRAV1(NPTFR,2) = IKLE(IELEM,SOMSUI(IFACE))
        ENDIF
     ENDDO
  ENDDO

  IERROR = 0
  DO I=1,NPTFR
     I1 = 1
     I2 = 1
     DO ISUIV=1,NPTFR
        IF (TRAV1(I,1).EQ.TRAV1(ISUIV,2)) I1 = I1 + 1
        IF (TRAV1(I,2).EQ.TRAV1(ISUIV,1)) I2 = I2 + 1
     ENDDO
  ENDDO

  SOM2 = X(1) + Y(1)
  Y2   = Y(1)

  DO I=1,NPTFR
     SOM1 = X(TRAV1(I,1)) + Y(TRAV1(I,1))
     IF (ABS(SOM1-SOM2).LE.ABS(EPSILO*SOM1)) THEN
        IF (Y(TRAV1(I,1)).LE.Y2) THEN
           Y2    = Y(TRAV1(I,1))
           SOM2  = SOM1
           ISUIV = I
        ENDIF
     ELSEIF (SOM1.LE.SOM2) THEN
        Y2    = Y(TRAV1(I,1))
        SOM2  = SOM1
        ISUIV = I
     ENDIF
  ENDDO
  
  NOEUD1 = TRAV1(ISUIV,1)
  NOEUD2 = TRAV1(ISUIV,2)
  TRAV1(ISUIV,1) = TRAV1(1,1)
  TRAV1(ISUIV,2) = TRAV1(1,2)
  TRAV1(1,1) = NOEUD1
  TRAV1(1,2) = NOEUD2
  
  IILE = 0
  NILE = 1

  DO I=2,NPTFR
     SWAP = .FALSE.
     DO ISUIV=I,NPTFR
        IF (TRAV1(ISUIV,1).EQ.TRAV1(I-1,2)) THEN
           NOEUD1 = TRAV1(ISUIV,1)
           NOEUD2 = TRAV1(ISUIV,2)
           TRAV1(ISUIV,1) = TRAV1(I,1)
           TRAV1(ISUIV,2) = TRAV1(I,2)
           TRAV1(I,1) = NOEUD1
           TRAV1(I,2) = NOEUD2
           KP1BOR(I+NPTFR) = I-1
           KP1BOR(I-1) = I
           SWAP = .TRUE.
           EXIT
        ENDIF
     ENDDO !ISUIV
     IF(SWAP) CYCLE
     IF (TRAV1(NILE,1).NE.TRAV1(I-1,2)) THEN
        WRITE(*,*) 'ERROR IN STORING EDGE SEGMENTS FOR NODE ', I
        CALL EXIT(0)
     END IF
     KP1BOR(NILE+NPTFR) = I-1
     KP1BOR(I-1) = NILE
     IILE = IILE+1
     NILE = I
  ENDDO ! I
  KP1BOR(NILE+NPTFR) = NPTFR
  KP1BOR(NPTFR) = NILE

  DO I=1,NPTFR
     NBOR(I      ) = TRAV1(I,1)
     NBOR(I+NPTFR) = TRAV1(I,2)
     !NCOLFR(I) = NCOLOR(TRAV1(I,1))
  ENDDO
  
  ! #########################################################################

  ! THIS IS THE PAT PRODANOVIC ADDITION
  ! WRITE THE FINAL OUTPUT TO THE TEXT FILE
  DO I=1,NPTFR
     WRITE(2,*) NBOR(I)
  END DO
  
END PROGRAM BND_EXTR_STBTEL
